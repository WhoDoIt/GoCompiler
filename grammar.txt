program     -> (declaration)* EOF 

declaration -> varDecl | statement

varDecl     -> "var" variable "=" expression ";"

statement   -> exprStmt | printStmt | ifstmt | forStmt | block

forStmt     -> "for" "(" varDecl expression ";" expression ")" statement
ifstmt      -> "if" "(" expression ")" statement
block       -> "{" declaration* "}"

exprStmt    -> expression ";"
printStmt   -> "print" expression ";"

expression  -> assignment
assignment  -> variable "=" expression | bitwise
bitwise     -> equality (("|" | "&") equality)*
equality    -> comparison (("==" | "!=") comparison)*
comparison  -> term ((">" | "<" | ">=" | "<=") term)*
term        -> factor (("+" | "-") factor)*
factor      -> unary (("/" | "*") unary)*
unary       -> ("!" | "-") unary | primary
primary     -> variable | STRING | NUMBER | "(" expression ")"
variable    -> IDENTIFIER